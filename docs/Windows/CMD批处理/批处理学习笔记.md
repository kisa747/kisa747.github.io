# 批处理笔记

## 笔记

参考：[CMD 命令速查手册](https://www.jb51.net/help/cmd.htm)

<http://kuanghy.github.io/2019/02/21/dos-cmd-note>

* 中文版 Windows 系统下，批处理文件应该采用 `cp936` 编码，即 `GBK` 或 `GB18030` 编码，如果要使用 `UTF8` 编码，需要在批处理前面添加 `chcp 65001` 的语句，会导致英文终端输出。
* 批处理应使用 `CR+LF` 的换行符，否则会报错。
* 两个百分号之间的内容为变量 `%variable%`，如果要表示百分号本身，使用 2 个百分号即可 `%vari%%able%`。

显示磁盘内所有的隐藏文件及目录：

```sh
# 显示 D 盘下所有的隐藏文件及目录
# /a:h 指的是显示具有隐藏属性的目录和文件的名称，a 和 h 之间的冒号是可选的。
# /b 指的是显示目录和文件的裸列表，无其他信息。
# /s 指的是列出指定目录和全部子目录中指定文件名的每次出现。
dir D: /a:h /b /s
```

## 特殊字符

```sh
for %%i in ('wmic LogicalDisk where "FileSystem='FAT32'" get DeviceID^,FileSystem 2^>NUL')
```

在 for 语句中，如果遍历对象是命令语句的结果，如果命令中包含 `,` `>` `|` 等关键字符，需要使用 `^` 进行转义。

常用特殊符号

|            命令            | 含义                                                         |
| :------------------------: | ------------------------------------------------------------ |
|            `@`             | 命令行回显屏蔽符                                             |
|            `%`             | 批处理变量引导符                                             |
| `>`、`>>`、`<`、`>&`、`<&` | 重定向符                                                     |
|            \|             | 命令管道符                                                   |
|            `^`             | 转义字符                                                     |
|            `&`             | 组合命令，这里 & 两边的命令是顺序执行的，从前往后执行。      |
|            `&&`            | 组合命令，当碰到执行出错的命令后将不再执行后面的命令，如果一直没有出错则一直执行完所有命令。 |
|            \|\|            | 组合命令，当一条命令执行失败后才执行第二条命令               |
|            `""`            | 字符串界定符                                                 |
|            `,`             | 逗号                                                         |
|            `;`             | 分号                                                         |
|            `()`            | 括号                                                         |
|            `!`             | 感叹号                                                       |
|            `:`             | 批处理标签引导符                                             |

## 包含空格的路径处理

批处理中路径不管是否有空格，都应该用双引号包裹起来，防止有空格造成运行错误。

示例：

```sh
cmd /k "pushd "%USERPROFILE%\my_venv_project" & "%USERPROFILE%\my_venv_project\.venv\Scripts\activate" && (echo 已进入虚拟环境... & python "%USERPROFILE%\my_venv_project\main.py")"
```

## choice 的使用示例

```sh
@echo off
::设置CMD窗口字体颜色为0a 在CMD中输入命令 color /? 可查看颜色列表
color 0a
::设置CMD窗口显示模式为100列宽 20行高
MODE con: COLS=100 LINES=20
echo  -------------------
echo    choice 命令示例
echo  -------------------
echo.
echo.
:: /c按键列表 /m提示内容 /d默认选择 /t等待秒数   /d 必须和 /t同时出现
choice  /c abcde /m "请输入" /d e /t 5

::用户选择的结果会按项目序号数字（从1开始）返回在errorlevel变量中
if %errorlevel%==1 echo 你选择了a
if %errorlevel%==2 echo 你选择了b
if %errorlevel%==3 echo 你选择了c
if %errorlevel%==4 echo 你选择了d
if %errorlevel%==5 echo 你选择了e
```

## errorlevel

`errorlevel` 与 `%errorlevel%` 都是判断上个命令的返回值。

```sh
if errorlevel 值 command
::表示：如果返回的错误码值大于或等于值 的时候，将执行cmmand

if %errorlevel%==值 cmmand
::表示：如果返回的错误码值等于值的时候，将执行cmmand操作。
```

if %ERRORLEVEL% 对数值的比较方法不仅仅限于等于，可以使用参数来控制。

比如：IF %ERRORLEVEL% LEQ 1 goto okay

全部的比较参数如下：

>EQU - 等于
>
>NEQ - 不等于
>
>LSS - 小于
>
>LEQ - 小于或等于
>
>GTR - 大于
>
>GEQ - 大于或等于

一般上一条命令的执行结果返回的值"成功"用 0 表示 "失败"用 1 表示，实际上，errorlevel 返回值可

以在 0~255 之间，

例如 xcopy 默认的 errorlevel 值就有 5 个，分别表示 5 种执行状态：

>0 复制文件成功
>
>1 未找到复制文件
>
>2 用户通过 CTRL C 终止了 xcopy 操作
>
>4 出现了初始化错误
>
>5 出现了磁盘写入错误

## if else 语句

参考：<https://www.yiibai.com/batch_script/batch_script_if_else_statement.html>

```sh
robocopy .\ .\source\images _config.yml
if errorlevel 1 (
  echo 正在清理 hexo 数据文件、渲染，然后发布博客。
  hexo clean & hexo d -g & pause
  ) else (
  echo 正在渲染，然后发布博客。
  hexo d -g & pause
)

# 如果测试字符串，需要双引号
if "%p:~-1%"==";"
```

## 字符串处理

### 字符串截取

```sh
@echo off
Setlocal enabledelayedexpansion
::CODER BY dsw POWERD BY iBAT
set abc=hello world, this string come from bat
echo 原字符串为:%abc%
echo 截取前5个字符:%abc:~0,5%
echo 截取最后5个字符:%abc:~-5%
echo 截取第一个到倒数第6个字符:%abc:~1,-5%
echo 从第4个字符开始截取5个字符:%abc:~3,5%
echo 从倒数第14个字符开始截取5个字符:%abc:~-14,5%
echo 当前时间是:%time% 即 %time:~0,2%点%time:~3,2%分%time:~6,2%秒%time:~9,2%厘秒
pause
```

输出为：

>原字符串为:hello world, this string come from bat
>截取前 5 个字符:hello
>截取最后 5 个字符:m bat
>截取第一个到倒数第 6 个字符:ello world, this string come fro
>从第 4 个字符开始截取 5 个字符:lo wo
>从倒数第 14 个字符开始截取 5 个字符：come
>当前时间是:18:37:58.75 即 18 点 37 分 58 秒 75 厘秒

### 字符串替换

```sh
@echo off
Setlocal enabledelayedexpansion
::CODER BY dsw POWERD BY iBAT

set aa=伟大的中国！我为你自豪
echo 替换前:%aa%
echo 替换后:%aa:中国=中华人民共和国%
echo aa=%aa%
echo %aa%
set aa=%aa:中国=中华人民共和国%
echo aa=%aa%

pause
```

输出为：

>替换前：伟大的中国！我为你自豪
>替换后：伟大的中华人民共和国！我为你自豪
>aa=伟大的中国！我为你自豪
>伟大的中国！我为你自豪
>aa=伟大的中华人民共和国！我为你自豪

### 字符串合并

```sh
@echo off
Setlocal enabledelayedexpansion
::CODER BY dsw POWERD BY iBAT

set aa=伟大的中国！
set bb=我为你自豪
echo %aa%%bb%

echo aa=%aa%
echo bb=%bb%
set "aa=%aa%%bb%"
echo aa=%aa%

pause

输出为：
伟大的中国！我为你自豪
aa=伟大的中国！
bb=我为你自豪
aa=伟大的中国！我为你自豪
```

## 字符串扩充

  “扩充”这个词汇来自于微软自己的翻译，意思就是对表示文件路径的字符串进行特殊的处理，具体功能罗列如下：

```sh
　　~I - 删除任何引号(")，扩充 %I
　　%~fI - 将 %I 扩充到一个完全合格的路径名
　　%~dI - 仅将 %I 扩充到一个驱动器号
　　%~pI - 仅将 %I 扩充到一个路径
　　%~nI - 仅将 %I 扩充到一个文件名
　　%~xI - 仅将 %I 扩充到一个文件扩展名
　　%~sI - 扩充的路径只含有短名
　　%~aI - 将 %I 扩充到文件的文件属性
　　%~tI - 将 %I 扩充到文件的日期/时间
　　%~zI - 将 %I 扩充到文件的大小
　　%~PATH:I−查找列在路径环境变量的目录，并将PATH:I−查找列在路径环境变量的目录，并将PATH:i - 查找列在路径环境变量的目录，并将 %I 扩充
　　到找到的第一个驱动器号和路径。
　　%~ftzaI - 将 %I 扩充到类似输出线路的 DIR
```

​    以上内容引用于 for /?帮助信息。其中的 I 代表变量 I，不过需要说明的是，不是所有的变量都能够进行扩充的，有两个条件：1、该字符串代表一个文件路径；2、变量要用%x来表示，x 可取 a-z A-Z 0-9 共 62 个字符中的任意一个。举例说明：

```bash
　　@echo off
　　echo 正在运行的这个批处理：
　　echo 完全路径：%0
　　echo 去掉引号：%~0
　　echo 所在分区：%~d0
　　echo 所处路径：%~p0
　　echo 文件名：%~n0
　　echo 扩展名：%~x0
　　echo 文件属性：%~a0
　　echo 修改时间：%~t0
　　echo 文件大小：%~z0
　　pause
```

其中的%0是批处理里面的参数，代表当前运行的批处理的完全路径。类似的还有%1-%9，分别代表传递来的第 1-9 个参数。
